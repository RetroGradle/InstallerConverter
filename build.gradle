plugins {
    id 'application'
}

group = 'uk.gemwire'
archivesBaseName = 'installerconverter'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.12.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.1'
    implementation group: 'com.google.jimfs', name: 'jimfs', version: '1.2'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'
}

application {
    mainClassName = 'uk.gemwire.installerconverter.Main'
}


run {
    workingDir = file('run/')
    workingDir.mkdirs()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

jar.configure {
    manifest {
        attributes([
                "Add-Exports": "java.base/sun.security.tools=ALL-UNNAMED " +
                        "jdk.jartool/sun.security.tools.jarsigner=ALL-UNNAMED " +
                        "java.base/sun.security.util=ALL-UNNAMED " +
                        "java.base/sun.security.x509=ALL-UNNAMED " +
                        "java.base/sun.security.pkcs=ALL-UNNAMED " +
                        "java.base/sun.security.timestamp=ALL-UNNAMED"
        ] as LinkedHashMap)
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll([
            "--add-exports", "java.base/sun.security.tools=ALL-UNNAMED",
            "--add-exports", "jdk.jartool/sun.security.tools.jarsigner=ALL-UNNAMED",
            "--add-exports", "java.base/sun.security.util=ALL-UNNAMED",
            "--add-exports", "java.base/sun.security.x509=ALL-UNNAMED",
            "--add-exports", "java.base/sun.security.pkcs=ALL-UNNAMED",
            "--add-exports", "java.base/sun.security.timestamp=ALL-UNNAMED"
    ])
    sourceCompatibility = 16;
    options.release = null;
}

test {
    useJUnitPlatform()
    workingDir = 'run/'
}